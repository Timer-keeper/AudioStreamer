plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.timer.audiostreamer"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"
        externalNativeBuild.cmake { 
            arguments "-DANDROID_STL=c++_shared"
        }
        ndk { 
            abiFilters 'arm64-v8a', 'x86_64'
        }
    }

    buildTypes {
        release { 
            minifyEnabled false 
        }
        debug { 
            debuggable true 
        }
    }

    // 在preBuild阶段解压Opus源码
    applicationVariants.all { variant ->
        variant.preBuildProvider.configure {
            doFirst {
                def opusDir = file("${rootProject.rootDir}/opus-1.3.1")
                // 如果目录不存在，尝试从tar解压
                if (!opusDir.exists()) {
                    def opusTar = file("${rootProject.rootDir}/opus-1.3.1.tar")
                    if (opusTar.exists()) {
                        copy {
                            from tarTree(opusTar)
                            into rootProject.rootDir  // 直接解压到项目根目录
                        }
                    } else {
                        throw new GradleException("opus-1.3.1.tar not found at ${opusTar.path}")
                    }
                }
            }
        }
    }

    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
}